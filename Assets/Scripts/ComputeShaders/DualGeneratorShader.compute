#ifndef Dual_Generator_
#define Dual_Generator_

#pragma kernel DualGenerator
#include "Includes/Mapper.compute"
#include "Includes/GPUTriangle.compute"
#include "Includes/MarchingCubes.compute"

/*Output*/
AppendStructuredBuffer<GPUTriangle> Triangles;

/*Inputs*/
int3 ThreadDimensions;
float CubesPerAxis;
RWStructuredBuffer<float4> NoisePoints;

float3 VertLerp(float4 vertex1, float4 vertex2)
{
    //return vertex1.xyz + (0 - vertex1.w) * (vertex2.xyz - vertex1.xyz) / (vertex2.w - vertex1.w);
    float t = (0.5f - vertex1.w) / (vertex2.w - vertex1.w);
    return vertex1.xyz + t * (vertex2.xyz - vertex1.xyz);
}

[numthreads(8, 8, 8)]
void DualGenerator(uint3 id : SV_DispatchThreadID)
{
    //Prevents out of bounds
    if (id.x >= CubesPerAxis - 1 || id.y >= CubesPerAxis - 1 || id.z >= CubesPerAxis - 1) return;

    //Grab cube noise data for current cube
    float4 cornerNodes[8] =
    {
        //Bottom
        NoisePoints[IndexByCoord(id.x, id.y, id.z, ThreadDimensions)],
        NoisePoints[IndexByCoord(id.x + 1, id.y, id.z, ThreadDimensions)],
        NoisePoints[IndexByCoord(id.x + 1, id.y, id.z + 1, ThreadDimensions)],
        NoisePoints[IndexByCoord(id.x, id.y, id.z + 1, ThreadDimensions)],
        //Top
        NoisePoints[IndexByCoord(id.x, id.y + 1, id.z, ThreadDimensions)],
        NoisePoints[IndexByCoord(id.x + 1, id.y + 1, id.z, ThreadDimensions)],
        NoisePoints[IndexByCoord(id.x + 1, id.y + 1, id.z + 1, ThreadDimensions)],
        NoisePoints[IndexByCoord(id.x, id.y + 1, id.z + 1, ThreadDimensions)]
    };

    //Find unique index for cube combination
    int triTableIndex = 0;
    if (cornerNodes[0].w < 0.5f) triTableIndex |= 1;
    if (cornerNodes[1].w < 0.5f) triTableIndex |= 2;
    if (cornerNodes[2].w < 0.5f) triTableIndex |= 4;
    if (cornerNodes[3].w < 0.5f) triTableIndex |= 8;
    if (cornerNodes[4].w < 0.5f) triTableIndex |= 16;
    if (cornerNodes[5].w < 0.5f) triTableIndex |= 32;
    if (cornerNodes[6].w < 0.5f) triTableIndex |= 64;
    if (cornerNodes[7].w < 0.5f) triTableIndex |= 128;

    for (int i = 0; TriTable[triTableIndex][i] != -1; i += 3)
    {
        int vertex1NodeA = VerticeNodeToCornerNodeIndex[TriTable[triTableIndex][i]][0];
        int vertex1NodeB = VerticeNodeToCornerNodeIndex[TriTable[triTableIndex][i]][1];

        int vertex2NodeA = VerticeNodeToCornerNodeIndex[TriTable[triTableIndex][i + 1]][0];
        int vertex2NodeB = VerticeNodeToCornerNodeIndex[TriTable[triTableIndex][i + 1]][1];

        int vertex3NodeA = VerticeNodeToCornerNodeIndex[TriTable[triTableIndex][i + 2]][0];
        int vertex3NodeB = VerticeNodeToCornerNodeIndex[TriTable[triTableIndex][i + 2]][1];

        GPUTriangle tri;
        //tri.vertexA = (cornerNodes[vertex1NodeA].xyz + cornerNodes[vertex1NodeB].xyz) * 0.5f;
        //tri.vertexB = (cornerNodes[vertex2NodeA].xyz + cornerNodes[vertex2NodeB].xyz) * 0.5f;
        //tri.vertexC = (cornerNodes[vertex3NodeA].xyz + cornerNodes[vertex3NodeB].xyz) * 0.5f;
        tri.vertexA = VertLerp(cornerNodes[vertex1NodeA], cornerNodes[vertex1NodeB]);
        tri.vertexB = VertLerp(cornerNodes[vertex2NodeA], cornerNodes[vertex2NodeB]);
        tri.vertexC = VertLerp(cornerNodes[vertex3NodeA], cornerNodes[vertex3NodeB]);
        Triangles.Append(tri);
    }
}
#endif